* hammond_278x_wht_blk.subckt
* Author: Will Marshall
* Created: 2/11/2026, 6:17:33 PM
*
* The following subckt model uses values as measured from my personal, new-stock
* Hammond 278X. The inductance measurements were performed around 60 Hz instead
* of the traditional 1 kHz LCR measurement, as the transformer will likely only
* ever see somewhere between 50 Hz and 60 Hz.
*
* The measurement procedure is as follows:
* 1) Using a handheld multimeter, measure the DCR of the coil under test.
* 2) Connect a low resistance resistor in series with the coil under test. The
*    resistance should be between 1 Ohm and ~200 Ohm.
* 3) Connect a signal generator from the free end of the resistor to the free
*    end of the coil under test.
* 4) Using an oscilloscope, connect channel 1 to the free end of the coil under
*    test. Connect channel 2 such that it probes the voltage between the coil
*    and the resistor. If the probes have ground clips, clip both ground leads
*    to the free end of the resistor, where the signal generator also connects.
* 5) Apply a sine wave of about 2 Vpp. The measurement will be repeated across
*    frequencies but start at a frequency of about 50 Hz.
* 6) Adjust the V/div on the o-scope for both channels such that you use about 
*    80% of the vertical space of the screen.
* 7) Adjust the s/div to capture a reasonable number of periods of the generated
*    waveform.
* 8) Using the math channel, show channel 1 - channel 2. This is the voltage 
*    across the coil. Adjust the V/div similarly to the analog channels to
*    maximize resolution.
* 9) Ensure the signal appearing across the resistor is not saturated with
*    noise, the waveform should be strong and of reasonable amplitude. Poor SNR
*    on the resistor measurement carries through to the coil amplitude
*    measurement. If a weak signal is observed, try a different resistor value
*    until you get solid signal. Take care to avoid selecting a resistor value
*    that makes the voltage across the inductor too small to reasonably measure.
* 10) Note the channel 2 RMS, the math channel RMS, and either channels
*     frequency. Ensure the frequencies between the two channels are in
*     agreement.
* 11) Repeat step 10 with small frequency increments. Aim for 3-5 steps over the
*     50-70 Hz band.
* 12) Disconnect all leads and probes, measure the resistance of the resistor
*     used for the measurement with a multimeter.
* 13) The inductance is then calculated as L=(R/(2*pi*f))*(V[L]/V[R]) where R is
*     the resistance in Ohms, f is the sine wave frequency in Hertz, V[L] is the
*     RMS voltage across the coil, and V[R] is the RMS voltage across the
*     resistor. L is given in Henrys.
*
* WHT - White lead (primary COM lead)
* BLK - 125V primary tap
* RED1 - Red lead (800V secondary end lead)
* REDYEL - "RED/YEL", red lead with yellow stripe, 800V secondary center tap
* RED2 - Red lead (800V secondary end lead), other end
* YEL1 - Yellow lead (5V secondary end lead)
* YELBLK - "YEL/BLK", yellow lead with black stripe, 5V secondary center tap
* YEL2 - Yellow lead (5V secondary end lead), other end
* GRN1 - Green lead (6.3V secondary end lead)
* GRNYEL - "GRN/YEL", green lead with yellow stripe, 6.3V secondary center tap
* GRN2 - Green lead (6.3V secondary end lead), other end
.SUBCKT HAMMOND_278X_WHT_BLK WHT BLK RED1 REDYEL RED2 YEL1 YELBLK YEL2 GRN1 
+ GRNYEL GRN2
* The DCR of the primary
.PARAM DCR_PRIM=1.7
* The self-inductance of the primary coil
.PARAM L_WHT_BLK=343m
* The DCR of half the HV secondary
.PARAM DCR_RED_REDYEL=46.4
* The self-inductance of half the HV secondary coil
.PARAM L_RED_REDYEL=2.8
* The DCR of half the 5V secondary
.PARAM DCR_YEL_YELBLK=50m
* The DCR of half the 6.3V secondary
.PARAM DCR_GRN_GRNYEL=60m
* The estimated turn ratio of half HV secondary to the primary
.PARAM TURN_RATIO_MAIN=3.2
* The estimated end-to-end self inductance of the 5V secondary coil
.PARAM L_YEL_YEL=623u
* The estimated end-to-end self-inductance of the 6.3V secondary coil
.PARAM L_GRN_GRN=1m
* Simulates the DCR of the primary inductor
R_PRIM WHT PRIM_COM {DCR_PRIM}
* The primary inductor
L_PRIM PRIM_COM BLK {L_WHT_BLK}
* The top half of the red secondary (800V winding) DCR
R_RED_SEC_1 RED1 RED_COM_1 {DCR_RED_REDYEL}
* The top half of the red secondary inductor
L_RED_SEC_1 RED_COM_1 REDYEL {L_RED_REDYEL}
* The bottom half of the red secondary DCR
R_RED_SEC_2 REDYEL RED_COM_2 {DCR_RED_REDYEL}
* The bottom half of the red secondary inductor
L_RED_SEC_2 RED_COM_2 RED2 {L_RED_REDYEL}
* Calculate the coupling constant
.PARAM K_SEC={SQRT((L_RED_REDYEL/L_WHT_BLK)/(TURN_RATIO_MAIN*TURN_RATIO_MAIN))}
* From here on, the inductance is calculated. There's no good way to measure
* the inductance with reasonably priced equipment on the heater coils. They just
* have far too low of a DCR. Take into consideration that all DCR measurements
* performed here include the length of the factory lead, which theoretically
* accounts for a larger proportion of the total DCR than the winding does on
* the heater coils.
* The inductance is estimated using the total self inductance measured of the heater
* coil. A low-voltage 60 Hz sinusoidal waveform is applied to the primary. The 
* relative amplitude between the primary and measured heater secondary gives the
* self inductance. With the estimation of the primary self-inductance from the
* measurement, we can estimate the self-inductance of the half heater windings.
.PARAM L_YEL_YELBLK={L_YEL_YEL/2}
.PARAM L_GRN_GRNYEL={L_GRN_GRN/2}
* The top half of the yellow secondary (5V winding) DCR
R_YEL_SEC_1 YEL1 YEL_COM_1 {DCR_YEL_YELBLK}
* The top half of the yellow secondary inductor
L_YEL_SEC_1 YEL_COM_1 YELBLK {L_YEL_YELBLK}
* The bottom half of the yellow secondary DCR
R_YEL_SEC_2 YELBLK YEL_COM_2 {DCR_YEL_YELBLK}
* The bottom half of the yellow secondary inductor
L_YEL_SEC_2 YEL_COM_2 YEL2 {L_YEL_YELBLK}
* The top half the green secondary (6.3V winding) DCR
R_GRN_SEC_1 GRN1 GRN_COM_1 {DCR_GRN_GRNYEL}
* The top half the green secondary inductor
L_GRN_SEC_1 GRN_COM_1 GRNYEL {L_GRN_GRNYEL}
* The bottom half the green secondary DCR
R_GRN_SEC_2 GRNYEL GRN_COM_2 {DCR_GRN_GRNYEL}
* The bottom half the green secondary inductor
L_GRN_SEC_2 GRN_COM_2 GRN2 {L_GRN_GRNYEL}
* Making the inductors act like a transformer, with k=0.89 as measured
K_RED_SEC1 L_PRIM L_RED_SEC_1 {K_SEC}
K_RED_SEC2 L_PRIM L_RED_SEC_2 {K_SEC}
K_YEL_SEC1 L_PRIM L_YEL_SEC_1 {K_SEC}
K_YEL_SEC2 L_PRIM L_YEL_SEC_2 {K_SEC}
K_GRN_SEC1 L_PRIM L_GRN_SEC_1 {K_SEC}
K_GRN_SEC2 L_PRIM L_GRN_SEC_2 {K_SEC}
.ENDS HAMMOND_278X_WHT_BLK